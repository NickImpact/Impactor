buildscript {
    repositories {
        mavenCentral()
        maven {
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
        maven {
            url = 'https://repo-new.spongepowered.org/repository/maven-snapshots'
        }
    }
    dependencies {
        classpath 'com.google.guava:guava:27.1-jre'
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

plugins {
    id 'com.qixalite.spongestart' version '1.6.2'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.kyori.blossom' version '1.1.0'
}

apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

group 'net.impactdev.impactor'
version '4.0.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

ext {
    spongeapiversion = '7.3'
    completeversion = "-S${spongeapiversion}-${project.version}"
}

jar {
    archiveName = "Impactor${completeversion}.jar"
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo-new.spongepowered.org/maven'
    }
    maven { url = 'https://jitpack.io' }
    maven {
        name 'drnaylor-minecraft'
        url 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
    maven {
        name 'drnaylor'
        url 'http://repo.drnaylor.co.uk/artifactory/list/quickstart'
    }
}

spongestart{
    eula true

    //optional settings, takes latest version by default
    minecraft '1.12.2'
}

dependencies {
    compile group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.kyori.blossom'
    apply plugin: 'org.cadixdev.licenser'


    group = 'net.impactdev.impactor'
    version = '4.0.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    project.ext.sponge = 'S8.0.0'
    project.ext.spigot = '1.12.2'

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
        maven { url = "https://repo.aikar.co/content/groups/aikar/" }
        maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    }

    dependencies {
        compile fileTree(dir: '../libs', include: '*.jar')
        shadow 'com.google.guava:guava:27.1-jre'
        shadow 'com.google.code.gson:gson:2.8.0'
        shadow "co.aikar:acf-core:0.5.0-SNAPSHOT"
        shadow 'net.kyori:adventure-api:4.8.0'
        shadow 'net.kyori:adventure-text-serializer-legacy:4.8.0'
        shadow 'net.kyori:adventure-text-serializer-gson:4.8.0'
        shadow 'net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    shadowJar {
        exclude "remapped*"
    }

    license {
        header = file('../HEADER.txt')
        ext {
            name = 'Impactor'
            url = 'https://github.com/NickImpact/Impactor/'
            year = 2021
        }
        newLine = true
    }
}

task copyJars(type: Copy) {
    dependsOn subprojects.build
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$buildDir/allJars"
    exclude '*-all.jar'
    exclude '*-sources.jar'
    exclude 'api*.jar'
    exclude 'common*.jar'
    exclude 'spigot*.jar'
    exclude 'sponge*.jar'
    exclude 'bungee*.jar'
    exclude '*API.jar'
    exclude '*Common.jar'
}

publishing {
    repositories {
        maven {
            name = "ImpactDev-Public"
            url = "https://maven.impactdev.net/repository/development/"
            credentials {
                username System.getenv('NEXUS_USER')
                password System.getenv('NEXUS_PW')
            }
        }
    }

    publications {
        api(MavenPublication) {
            def project = project(':api')
            from project.components.java
            groupId project.group
            artifactId "api"
            version project.version
        }

        common(MavenPublication) {
            def project = project(':common')
            from project.components.java
            groupId project.group
            artifactId "common"
            version project.version
        }

        bungee(MavenPublication) {
            def project = project(':bungee')
            from project.components.java
            groupId project.group
            artifactId "bungee"
            version project.version
        }

        velocity(MavenPublication) {
            def project = project(':velocity')
            from project.components.java
            groupId project.group
            artifactId "velocity"
            version project.version
        }

//        spigot(MavenPublication) {
//            def project = project(':spigot')
//            from project.components.java
//            groupId project.group
//            artifactId "spigot"
//            version project.version
//        }

        sponge(MavenPublication) {
            def project = project(':sponge')
            from project.components.java
            groupId project.group
            artifactId "sponge"
            version "8.0.0-" + project.version
        }
    }
}

build.dependsOn copyJars