buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://repository.jboss.org/nexus/content/groups/public/"
        }
        maven {
            url = 'https://repo-new.spongepowered.org/repository/maven-snapshots'
        }
    }
    dependencies {
        classpath 'com.google.guava:guava:27.1-jre'
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.kyori.blossom' version '1.3.0'
}

apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

group 'net.impactdev.impactor'
version '5.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo-new.spongepowered.org/maven'
    }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    api group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.kyori.blossom'
    apply plugin: 'org.cadixdev.licenser'

    group = 'net.impactdev.impactor'
    version = '5.0.0-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }

        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
        maven { url = "https://repo.aikar.co/content/groups/aikar/" }
        maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    }

    dependencies {
        // Google
        shadow 'com.google.guava:guava:27.1-jre'
        shadow 'com.google.code.gson:gson:2.8.0'

        // Configurate
        shadow 'org.spongepowered:configurate-core:4.1.2'
        shadow 'org.spongepowered:configurate-gson:4.1.2'
        shadow 'org.spongepowered:configurate-hocon:4.1.2'
        shadow 'org.spongepowered:configurate-yaml:4.1.2'

        // Adventure
        shadow 'net.kyori:adventure-api:4.10.1'
        shadow 'net.kyori:adventure-text-serializer-legacy:4.10.1'
        shadow 'net.kyori:adventure-text-serializer-gson:4.10.1'
        shadow 'net.kyori:adventure-text-minimessage:4.10.1'

        // Events
        api 'net.kyori:event-api:3.0.0'
        api 'net.kyori:event-method:3.0.0'
        api 'net.kyori:event-method-asm:3.0.0'

        // Netty
        shadow 'io.netty:netty-all:4.1.25.Final'

        // Misc
        shadow 'org.reflections:reflections:0.10.2'
        shadow group: 'org.spongepowered', name: 'math', version: '2.0.1'

        // Testing
        testImplementation group: 'junit', name: 'junit', version: '4.12'
    }

    shadowJar {
        exclude "remapped*"
    }

    license {
        header = file('../HEADER.txt')
        ext {
            name = 'Impactor'
            url = 'https://github.com/NickImpact/Impactor/'
            year = 2022
        }
        newLine = true
    }

    compileJava.dependsOn(updateLicenses)
}

task copyJars(type: Copy) {
    dependsOn subprojects.build
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$buildDir/allJars"
    exclude '*-all.jar'
    exclude '*-sources.jar'
    exclude 'api*.jar'
    exclude 'common*.jar'
    exclude 'sponge*.jar'
    exclude 'bungee*.jar'
    exclude 'forge*.jar'
    exclude '*API.jar'
    exclude '*Common.jar'
    exclude 'velocity*.jar'
    exclude 'launcher*.jar'
    exclude '*.jarinjar'
}

publishing {
    repositories {
        maven {
            name = "ImpactDev-Public"
            url = "https://maven.impactdev.net/repository/development/"
            credentials {
                username System.getenv('NEXUS_USER')
                password System.getenv('NEXUS_PW')
            }
        }
    }

    publications {
        api(MavenPublication) {
            def project = project(':api')
            from project.components.java
            groupId project.group
            artifactId "api"
            version project.version
        }

        launcher(MavenPublication) {
            def project = project(':launcher')
            from project.components.java
            groupId project.group
            artifactId "launcher"
            version project.version
        }

//        common(MavenPublication) {
//            def project = project(':common')
//            from project.components.java
//            groupId project.group
//            artifactId "common"
//            version project.version
//        }

//        bungee(MavenPublication) {
//            def project = project(':bungee')
//            from project.components.java
//            groupId project.group
//            artifactId "bungee"
//            version project.version
//        }

//        velocity(MavenPublication) {
//            def project = project(':velocity')
//            from project.components.java
//            groupId project.group
//            artifactId "velocity"
//            version project.version
//        }

//        sponge(MavenPublication) {
//            def project = project(':sponge')
//            from project.components.java
//            groupId project.group
//            artifactId "sponge"
//            version project.version
//        }
    }
}

build.dependsOn copyJars