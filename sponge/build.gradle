import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

plugins {
    id 'org.spongepowered.gradle.plugin' version '2.0.0'
}

apply plugin: 'net.minecraftforge.gradle'

ext {
    sponge = "8.0.0"
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'official', version: '1.16.5'
}

sponge {
    apiVersion("8.0.0-SNAPSHOT")
    license("GNU General Public License v3.0")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin("impactor") {
        displayName("Impactor")
        entrypoint("net.impactdev.impactor.sponge.SpongeImpactorPlugin")
        description("Multi-platform utility API")
        links {
            source("https://github.com/NickImpact/Impactor")
            issues("https://github.com/NickImpact/Impactor/issues")
        }
        contributor("NickImpact") {
            description("Lead Developer")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name 'Sponge Snapshots'
        url 'https://repo.spongepowered.org/repository/maven-snapshots'
    }
    maven {
        name 'Sponge Releases'
        url 'https://repo.spongepowered.org/repository/maven-releases'
    }
    maven {
        name 'Sponge Legacy Repository'
        url 'https://repo.spongepowered.org/repository/sponge-legacy'
    }
    maven { url = 'https://jitpack.io' }
    maven {
        name 'drnaylor-minecraft'
        url 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
    maven {
        name 'drnaylor'
        url 'http://repo.drnaylor.co.uk/artifactory/list/quickstart'
    }
    maven {
        name = "Impact-Dev"
        url = "https://maven.impactdev.net/repository/development/"
    }
}

dependencies {
    compile project(':api')
    compile project(':common')

    minecraft 'net.minecraftforge:forge:1.16.5-36.0.48'

    shadow 'org.spongepowered:math:2.0.0'
   // shadow "com.ichorpowered:protocolcontrol:0.0.3"
    shadow 'me.lucko:jar-relocator:1.5'
    compile("net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT") {
        exclude(module: 'adventure-api')
        exclude(module: 'adventure-key')
    }
}

blossom {
    blossomSourceReplacementJava.output = file("$buildDir/sources/java")
    replaceToken '@version@', version
}

shadowJar {
    archiveName = "Impactor-Sponge-${sponge}-${version}.jar"

    exclude 'META-INF/versions/9/module-info.class'
    exclude 'module-info.class'

    dependencies {
        include(project(':api'))
        include(project(':common'))
        include(dependency('net.kyori:adventure-text-minimessage:.*'))
    }

    // Shaded Components
//    relocate 'com.github.ben-manes', 'com.nickimpact.impactor.relocations.com.github.ben-manes'
//    relocate 'me.lucko.jarrelocator', 'com.nickimpact.impactor.relocations.me.lucko.jarrelocator'
    relocate 'net.bytebuddy', 'net.impactdev.impactor.relocations.bytebuddy'

    // Setup Kyori Event Shading. Due to additional shading made by Protocol Control, we need to handle
    // the shading specifically here.
    //
    // In other words, we should only attempt to relocate everything but the Subscribe annotation,
    // as Impactor uses it's own version.
    relocate ('net.kyori.event', 'net.impactdev.impactor.relocations.eventbus') {
        exclude 'net.kyori.event.method.annotation.Subscribe'
    }

    relocate 'net.kyori.adventure.text.minimessage', 'net.impactdev.impactor.relocations.minimessage'
    relocate("net.kyori.event", "com.ichorpowered.protocolcontrol.lib.kyori")

    // Storage Based Relocations
    relocate ('com.zaxxer.hikari', 'net.impactdev.impactor.relocations.hikari')
    relocate ('org.mariadb.jdbc', 'net.impactdev.impactor.relocations.mariadb')
    relocate ('com.mysql', 'net.impactdev.impactor.relocations.mysql')
}

reobf {
    shadowJar {}
}

build.dependsOn reobfShadowJar
