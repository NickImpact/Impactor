import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id 'org.spongepowered.gradle.plugin' version '2.0.0'
    id 'org.spongepowered.gradle.vanilla' version '0.2'
}

ext {
    sponge = "8.0.0"
}

minecraft {
    version("1.16.5")
    runs {
        //server()
        client()
    }
}

sponge {
    apiVersion("8.0.0")
    license("MIT")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin("impactor") {
        displayName("Impactor")
        entrypoint("net.impactdev.impactor.sponge.SpongeImpactorPlugin")
        description("Multi-platform utility API")
        links {
            source("https://github.com/NickImpact/Impactor")
            issues("https://github.com/NickImpact/Impactor/issues")
        }
        contributor("NickImpact") {
            description("Lead Developer")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name 'Sponge Snapshots'
        url 'https://repo.spongepowered.org/repository/maven-snapshots'
    }
    maven {
        name 'Sponge Releases'
        url 'https://repo.spongepowered.org/repository/maven-releases'
    }
    maven { url = 'https://jitpack.io' }
    maven {
        name = "Impact-Dev"
        url = "https://maven.impactdev.net/repository/development/"
    }
}

dependencies {
    api project(':api')
    api project(':common')

    compileOnly fileTree(dir: 'libs', include: '*.jar')

    shadow 'com.github.ben-manes.caffeine:caffeine:2.8.4'
    shadow 'me.lucko:jar-relocator:1.5'
}

blossom {
    replaceToken '@version@', version
}

shadowJar {
    archiveName = "Impactor-Sponge-${sponge}-${version}.jar"

    dependencies {
        include(project(':api'))
        include(project(':common'))
        include(dependency('net.kyori:adventure-text-minimessage:.*'))
    }

    // Shaded Components
//    relocate 'com.github.ben-manes', 'com.nickimpact.impactor.relocations.com.github.ben-manes'
//    relocate 'me.lucko.jarrelocator', 'com.nickimpact.impactor.relocations.me.lucko.jarrelocator'
    relocate 'net.bytebuddy', 'net.impactdev.impactor.relocations.bytebuddy'

    // Setup Kyori Event Shading. Due to additional shading made by Protocol Control, we need to handle
    // the shading specifically here.
    //
    // In other words, we should only attempt to relocate everything but the Subscribe annotation,
    // as Impactor uses it's own version.
    relocate ('net.kyori.event', 'net.impactdev.impactor.relocations.eventbus') {
        exclude 'net.kyori.event.method.annotation.Subscribe'
    }

    relocate 'net.kyori.adventure.text.minimessage', 'net.impactdev.impactor.relocations.minimessage'
    relocate("net.kyori.event", "com.ichorpowered.protocolcontrol.lib.kyori")

    // Storage Based Relocations
    relocate ('com.zaxxer.hikari', 'net.impactdev.impactor.relocations.hikari')
    relocate ('org.mariadb.jdbc', 'net.impactdev.impactor.relocations.mariadb')
    relocate ('com.mysql', 'net.impactdev.impactor.relocations.mysql')
    relocate ('com.github.benmanes.caffeine', 'net.impactdev.impactor.relocations.caffeine')
}

build.dependsOn shadowJar